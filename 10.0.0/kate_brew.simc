#!./simc
iterations=100000
target_error=0.25

# Kateborf - Brewmaster - 2022-10-14 17:17 - us/Valdrakken
# SimC Addon 10.0.0-alpha-07
# WoW 10.0.2.46092, TOC 100002
# Requires SimulationCraft 1000-01 or newer

monk="Kateborf"
level=70
race=dwarf
region=us
server=valdrakken
role=tank
professions=blacksmithing=100/leatherworking=100
spec=brewmaster
position=front

class_talents=101412:2/101441:1/101442:1/101444:1/101448:1/101449:1/101451:1/101452:2/101453:1/101454:1/101455:1/101461:1/101462:1/101463:1/101464:1/101465:1/101467:1/101471:1/101496:1/101497:1/101501:1/101503:1/101504:1/101505:1/101506:1/101508:1/101509:1/101511:1/101512:1/101515:1/101516:1/101519:1/101520:2/101521:2/101522:1/101523:2/101524:2/101525:2/101526:1/101528:1/101529:2/101530:1/101531:1/101537:1/101539:1/101541:1/101542:1/101544:1/101545:1/101547:1/101548:1/101551:1/101552:1


head=,id=193400,bonus_id=8836/8840/8902/8802/8795,crafted_stats=32/40
neck=,id=201759,gem_id=192925/192925/192925,bonus_id=8836/8840/8902/8783/8782/8802/8795,crafted_stats=36/49
shoulder=,id=200365,bonus_id=1463/8767
back=,id=153865,enchant_id=6604,bonus_id=8942/3219
chest=,id=200360,enchant_id=6616,bonus_id=1466/8767
wrist=,id=193419,enchant_id=6586,bonus_id=8836/8840/8902/8802/8795,crafted_stats=40/49
hands=,id=200362,bonus_id=1466/8767
waist=,id=193407,bonus_id=8836/8840/8902/8802/8797/8960/8795,crafted_stats=32/40
legs=,id=200364,enchant_id=6496,bonus_id=1472/8767
feet=,id=193452,enchant_id=6607,bonus_id=8836/8840/8902/8960/8802
finger1=,id=193000,enchant_id=6550,gem_id=192925,bonus_id=8836/8840/8902/8780/8802/8795,crafted_stats=36/40
finger2=,id=192999,enchant_id=6550,gem_id=192925,bonus_id=8836/8840/8902/8780/8802/8795,crafted_stats=40/49
trinket1=,id=193003,bonus_id=8836/8840/8902/8802
trinket2=,id=193757,bonus_id=8967/7977/6652/1617/8767
main_hand=,id=190513,enchant_id=6649,bonus_id=8836/8840/8902/8802/8795,crafted_stats=49/40
off_hand=,id=190513,enchant_id=6648,bonus_id=8836/8840/8902/8802/8795,crafted_stats=49/32

#actions.precombat+=/snapshot_stats

actions=auto_attack

actions+=/spear_hand_strike,if=target.debuff.casting.react
actions+=/gift_of_the_ox,if=health<health.max*0.65
actions+=/dampen_harm,if=incoming_damage_1500ms&buff.fortifying_brew.down
actions+=/fortifying_brew,if=incoming_damage_1500ms&(buff.dampen_harm.down|buff.diffuse_magic.down)
actions+=/potion
actions+=/blood_fury
actions+=/berserking
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks
actions+=/touch_of_death,if=target.health.pct<=15
actions+=/call_action_list,name=improved_niuzao,if=talent.improved_invoke_niuzao.enabled
actions+=/call_action_list,name=base_niuzao,if=!talent.improved_invoke_niuzao.enabled
actions+=/bonedust_brew,if=!debuff.bonedust_brew_debuff.up
# Black Ox Brew is currently used to either replenish brews based on less than half a brew charge available, or low energy to enable Keg Smash
actions+=/black_ox_brew,if=cooldown.purifying_brew.charges_fractional<0.5
actions+=/black_ox_brew,if=(energy+(energy.regen*cooldown.keg_smash.remains))<40&buff.blackout_combo.down&cooldown.keg_smash.up
# Offensively, the APL prioritizes KS on cleave, BoS else, with energy spenders and cds sorted below
actions+=/keg_smash,if=spell_targets>=2
# cast KS at top prio during WoO buff
actions+=/keg_smash,if=buff.weapons_of_order.up
# Celestial Brew priority whenever it took significant damage (adjust the health.max coefficient according to intensity of damage taken), and to dump excess charges before BoB.
actions+=/celestial_brew,if=buff.blackout_combo.down&incoming_damage_1999ms>(health.max*0.1+stagger.last_tick_damage_4)&buff.elusive_brawler.stack<2
actions+=/rising_sun_kick
actions+=/exploding_keg
actions+=/tiger_palm,if=talent.rushing_jade_wind.enabled&buff.blackout_combo.up&buff.rushing_jade_wind.up
actions+=/breath_of_fire,if=buff.charred_passions.down&talent.charred_passions.enabled
actions+=/blackout_kick
actions+=/keg_smash
actions+=/faeline_stomp
actions+=/rushing_jade_wind,if=buff.rushing_jade_wind.down
actions+=/spinning_crane_kick,if=buff.charred_passions.up
actions+=/breath_of_fire,if=buff.blackout_combo.down&(buff.bloodlust.down|(buff.bloodlust.up&dot.breath_of_fire_dot.refreshable))
actions+=/chi_burst
actions+=/chi_wave
# Cast SCK if enough enemies are around, or if WWWTO is enabled. This is a slight defensive loss over using TP but generally reduces sim variance more than anything else.
actions+=/spinning_crane_kick,if=active_enemies>=3&cooldown.keg_smash.remains>gcd&(energy+(energy.regen*(cooldown.keg_smash.remains+execute_time)))>=65
actions+=/tiger_palm,if=!talent.blackout_combo.enabled&cooldown.keg_smash.remains>gcd&(energy+(energy.regen*(cooldown.keg_smash.remains+gcd)))>=65
actions+=/arcane_torrent,if=energy<31
actions+=/rushing_jade_wind


## Sub-APL for handling improved invoke niuzao and CtA
# Cast Niuzao when we'll get at least 20 seconds of uptime. This is specific to the default enemy APL and will need adjustments for other enemies.
actions.improved_niuzao=invoke_niuzao_the_black_ox,if=buff.recent_purifies.value>=health.max*0.05&(target.cooldown.pause_action.remains>=20|time<=10|target.cooldown.pause_action.duration=0)&(!talent.call_to_arms.enabled|cooldown.weapons_of_order.remains>25)&!buff.invoke_niuzao_the_black_ox.up
# Cast Niuzao if we just lost the Niuzao from CtA
actions.improved_niuzao+=/invoke_niuzao_the_black_ox,if=buff.weapons_of_order.up&talent.call_to_arms.enabled&!buff.invoke_niuzao_the_black_ox.up
# Use WoO on CD unless we have CtA equipped, in which case we treat it as mini-Niuzao not WoO.
actions.improved_niuzao+=/weapons_of_order,if=!talent.call_to_arms.enabled|(buff.recent_purifies.value>=health.max*0.05&(target.cooldown.pause_action.remains>=20|time<=10|target.cooldown.pause_action.duration=0)&!buff.invoke_niuzao_the_black_ox.up)
# Cast PB during the Niuzao window, but only if recently hit.
actions.improved_niuzao+=/purifying_brew,if=stagger.amounttototalpct>=0.7&(((target.cooldown.pause_action.remains>=20|time<=10|target.cooldown.pause_action.duration=0)&cooldown.invoke_niuzao_the_black_ox.remains<5)|buff.invoke_niuzao_the_black_ox.up)
# Dump PB charges towards the end of Niuzao: anything is better than nothing.
actions.improved_niuzao+=/purifying_brew,if=buff.invoke_niuzao_the_black_ox.up&buff.invoke_niuzao_the_black_ox.remains<8
# Avoid capping charges, but pool charges shortly before Niuzao comes up and allow dumping to avoid capping during Niuzao.
actions.improved_niuzao+=/purifying_brew,if=cooldown.purifying_brew.charges_fractional>=1.8&(cooldown.invoke_niuzao_the_black_ox.remains>10|buff.invoke_niuzao_the_black_ox.up)

## When you don't have improved invoke, this is dramatically simpler
actions.base_niuzao=weapons_of_order
actions.base_niuzao+=/invoke_niuzao_the_black_ox
actions.base_niuzao+=/purifying_brew,if=stagger.amounttototalpct>=0.7
actions.base_niuzao+=/purifying_brew,if=cooldown.purifying_brew.remains_expected<3
